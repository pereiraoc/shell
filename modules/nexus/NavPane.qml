import "navpane"
import QtQuick
import QtQuick.Layouts
import Caelestia.Config
import qs.components
import qs.components.controls
import qs.services
import qs.modules.nexus

ColumnLayout {
    id: root

    required property NexusState nState

    spacing: Tokens.spacing.large

    SearchBar {
        id: searchField

        Layout.fillWidth: true

        placeholderText: qsTr("Search settings")
        font: Tokens.font.body.large

        bg.color: Colours.tPalette.m3surfaceContainerLowest
        bg.border.color: Colours.palette.m3outlineVariant
        searchIcon.fontStyle: Tokens.font.icon.medium
        searchIcon.anchors.leftMargin: Tokens.padding.largeIncreased
        clearIcon.font: Tokens.font.icon.medium
        clearIcon.padding: Tokens.padding.extraSmall

        Behavior on bg.border.color {
            CAnim {}
        }

        Binding {
            target: root.nState
            property: "searchOpen"
            value: searchField.text.trim().length > 0
        }

        Binding {
            target: root.nState
            property: "searchText"
            value: searchField.text
        }

        // Enter abre o melhor resultado; Esc limpa a busca.
        onAccepted: results.openFirst()
        Keys.onEscapePressed: event => {
            if (searchField.text.length > 0) {
                searchField.text = "";
                event.accepted = true;
            } else {
                event.accepted = false;
            }
        }
    }

    NavLocations {
        Layout.fillWidth: true
        Layout.fillHeight: true
        Layout.topMargin: -topMargin
        Layout.bottomMargin: -bottomMargin
        visible: !root.nState.searchOpen
        nState: root.nState
    }

    SearchResults {
        id: results

        Layout.fillWidth: true
        Layout.fillHeight: true
        Layout.topMargin: -topMargin
        Layout.bottomMargin: -bottomMargin
        visible: root.nState.searchOpen
        nState: root.nState
        query: searchField.text
        onPicked: searchField.text = ""
    }
}
